/***************************************************
 * MADX
 *
 * This madx file is used for:
 * ! Qh=20.13, QV=20.18 - no qsplit - chromaticity (0.0/0.0)  - SPS units
 *
 * SPS_Q20.str is used for the LHC beam:
 * 1) at injection (26 GeV/c, not 14)
 * 2) during the ramp
 * 3) and at extraction (450 GeV/c)
 *
 * Call the file by: >madx < sps_Q20.madx
 ***************************************************/

!system,"ln -snf /afs/cern.ch/eng/sps/2015 sps";

 option, -echo;



/***************************************************
 * SPECIFY THE SPS BEAM CONFIGURATION, ENERGY and EMITTANCES
 ***************************************************/

 call, file = 'sps/beams/lhc_beam_injection.beamx';



/***************************************************
 * files for SPS
 ***************************************************/

 call, file = 'sps/elements/sps.ele';
 call, file = 'sps/aperture/aperturedb_1.dbx';
 call, file = 'sps/aperture/aperturedb_2.dbx';
 call, file = 'sps/sequence/sps.seq';
 !call, file = 'sps/aperture/aperturedb_3.dbx';
 call, file = 'sps/strength/SPS_Q20.str';
 call, file = 'sps/strength/elements.str';
 option, echo;


/***************************************************
 * SEQEDIT
 * Junction between TT10 and SPS
 ***************************************************/

/*
SEQEDIT, SEQUENCE=SPS;
ENDTT10 : MARKER;
install, element = ENDTT10, at = -0.1375, from = BPH.12008;
ENDEDIT;

SEQEDIT, SEQUENCE=SPS;
ENDVKNV : MARKER;
install, element = ENDVKNV, at = 10.3015+0.1755, from = BPD.11833;
ENDEDIT;

SEQEDIT, SEQUENCE=SPS;
flatten ; cycle, start=  ENDVKNV;
ENDEDIT;
*/



/***************************************************
 * use
 ***************************************************/

 use, sequence=SPS;




/***************************************************
 * TWISS
 ***************************************************/

 set,  format="11.8f";
 set,  format="-18s";

 select, flag=twiss, clear;
 select, flag=twiss
       , column=name,keyword,s,l,angle,k0l,k1l,k2l,x,px,alfx,betx,dx,mux,y,py,alfy,bety,dy,muy;

 twiss, DELTAP=0.000,  centre;
!twiss, DELTAP=0.000;


/***************************************************
 * Write twiss table.
 ***************************************************/

 write,table=twiss,file="twiss_SPS_Q20.tfs";

!write,table=twiss;
!value,table(twiss,SPS$END,dx);

/***************************************************
 * Build thin model
 ***************************************************/
 
 USE, period=SPS, range=#S/#E;
 select, flag=seqedit, class=instrument;
 select, flag=seqedit, class=marker;
 seqedit, sequence=SPS;
	remove, element=BEGI.10010;		!zero length element
	remove, element=VVFB.21801;		!zero length element
	remove, element=VVFB.21877;		!zero length element
	remove, element=QSPL.31809;		!zero length element
	remove, element=VVFB.61801;		!zero length element
	remove, element=QSPL.61809;		!zero length element
	remove, element=VVFB.61877;		!zero length element
	remove, element=selected;
	flatten;
 endedit;
 USE, period=SPS, range=#S/#E;
 
 use, sequence=sps;
 select, flag=makethin, slice=10, thick=false;
 makethin, sequence=sps, style=teapot, makedipedge=false;
 use, sequence=SPS;
 twiss;

 ! install markers at locations of sc kicks
 option, -info;
 n_kicks_target = 997.; ! might not be reached exactly with randomization
 length = table(summ,length);
 sc_length = length/(n_kicks_target);
 sc_placeholder : Marker; 
 random := sc_length/5. * tgauss(2);
 s = sc_length;

 seqedit, sequence=SPS;
 while(s < length-sc_length/2.){
 	install, element=sc_placeholder, at=s;
 	value, s;
 	s = s + sc_length + random;
 };
 install, element=sc_placeholder, at=length;
 flatten;
 option, info;
 endedit;

 use, period=SPS, range=#S/#E;
 twiss;

 match, sequence=sps;
 vary, name=kqd, step=1e-8;
 vary, name=kqf1, step=1e-8;
 global, q1=20.13, q2=20.18;
 lmdif, calls=1000, tolerance=1e-22;
 endmatch;

 twiss;

 write,table=twiss,file="twiss_SPS_Q20_thin.tfs";

 save, beam, sequence=sps,file="SPS_Q20_thin.seq";

 vacta31637= 4.5;

use,sequence=sps; twiss;
sixtrack, cavall, radius=0.017;

 stop;





